program     = statement* EOF ;

declaration = varDecl | statement;

varDecl     = "var" IDENTIFIER ( "=" expression )? ";" ;

statement   = exprStmt
            | forStmt
            | ifStmt
            | printStmt
            | whileStmt
            | block ;
exprStmt    = expression ";" ;
ifStmt      = "if" "(" expression ")" statement ( "else" statement )? ;
printStmt   = "print" expression ";" ;
whileStmt   = "while" "(" expression ")" statement ;
forStmt     = "for" "(" ( varDecl | exprStmt | ";" )
              expression? ";"
              expression? ")" statement ;
block       = "{" declaration* "}";

expression  = assignment ;
assignment  = IDENTIFIER "=" assignment
            | logicOr ;
logicOr     = logicAnd ( "or" logicAnd )* ;
logicAnd    = equality ( "and" equality )* ;
equality    = comparison ( ( "!=" | "==" ) comparison )* ;
comparison  = term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term        = factor ( ( "-" | "+" ) factor )* ;
factor      = unary ( ( "/" | "*" ) unary )* ;
unary       = ( "!" | "-" ) unary
            | primary ;
primary     = NUMBER | STRING
            | "true" | "false" | "nil"
            | "(" expression ")"
            | IDENTIFIER ;